import groovy.transform.Field

// hardcoded secret paths in vault
@Field private static final String secretKeystoreFileData = "secret/android/keystore_file_data"
@Field private static final String secretKeystorePassword = "secret/android/keystore_password"
@Field private static final String secretKeyAlias = "secret/android/key_alias"
@Field private static final String secretKeyPassword = "secret/android/key_password"

/**
 * Utility function to check if a file can be read from this process
 * @param file
 * @return
 */
static boolean fileReadable(File file) {
    if (!file.exists() || !file.canRead())
        return false

    try {
        FileReader fileReader = new FileReader(file.getAbsolutePath())
        fileReader.read()
        fileReader.close()
    } catch (Exception ignored) {
        return false
    }
    return true
}

/**
 * rewrite a file with random bytes
 * @param tempFile
 */
static void fillWithGarbage(File tempFile) {
    if (tempFile == null) {
        return
    }
    byte[] buffer = new byte[tempFile.size()]
    RandomAccessFile raf = new RandomAccessFile(tempFile, "rw")
    raf.seek(0)
    new Random().nextBytes(buffer)
    raf.write(buffer)
    raf.close()
}

project.ext.vaultKeystoreTempPath = null

def clearFields() {
    project.ext.signingProps = null
}

def getReleaseKeystore() {
    checkVaultEnvironment()
    String keystoreFile
    String b64KeystoreFileData

    if (project.vaultKeystoreTempPath != null) {
        return file(vaultKeystoreTempPath)
    } else if (project.signingProps?.get("storeFile") != null) {
        keystoreFile = project.signingProps?.get("storeFile")
    } else if (project.hasProperty("IGNORE_VAULT")) {
        //build release using dummy signature
        keystoreFile = "../keystores/dummy-release.keystore"
    } else {
        try {
            b64KeystoreFileData = project?.vault?.get(secretKeystoreFileData.toString())['data']['value']
        } catch (Exception e) {
            throw new InvalidUserDataException("There was an error while reading the keystore data from vault.\n" +
                    "Make sure to have $secretKeystoreFileData in the vault.\n" +
                    "Run this task with -PIGNORE_VAULT to skip secrets and build a release apk using a dummy signature", e)
        }
        vaultKeystoreTempPath = File.createTempFile("datapipe-ks", ".jksdump").path
        byte[] keystoreBytes = Base64.decoder.decode(b64KeystoreFileData)

        FileOutputStream stream = new FileOutputStream(vaultKeystoreTempPath)
        try {
            stream.write(keystoreBytes)
        } finally {
            stream.close()
        }
        keystoreFile = vaultKeystoreTempPath
    }
    return file(keystoreFile)
}

def getReleaseKeystorePassword() {
    checkVaultEnvironment()
    String keystorePassword
    if (project.signingProps?.get("storePassword") != null) {
        keystorePassword = project.signingProps?.get("storePassword")
    } else if (project.hasProperty("IGNORE_VAULT")) {
        //build release using dummy signature
        keystorePassword = "dummypassword"
    } else {
        try {
            keystorePassword = project?.vault?.get(secretKeystorePassword)['data']['value']
        } catch (Exception e) {
            throw new InvalidUserDataException("There was an error while reading the keystore password from vault.\n" +
                    "Make sure to have $secretKeystorePassword in the vault.\n" +
                    "Run this task with -PIGNORE_VAULT to skip secrets and build a release apk using a dummy signature", e)
        }
    }
    return keystorePassword
}


def getReleaseKeyAlias() {
    checkVaultEnvironment()
    String keyAlias
    if (project.signingProps?.get("keyAlias") != null) {
        keyAlias = project.signingProps?.get("keyAlias")
    } else if (project.hasProperty("IGNORE_VAULT")) {
        //build release using dummy signature
        keyAlias = "dummy-alias"
    } else {
        try {
            keyAlias = project?.vault?.get(secretKeyAlias)['data']['value']
        } catch (Exception e) {
            throw new InvalidUserDataException("There was an error while reading the key alias from vault.\n" +
                    "Make sure to have $secretKeyAlias in the vault.\n" +
                    "Run this task with -PIGNORE_VAULT to skip secrets and build a release apk using a dummy signature", e)
        }
    }
    return keyAlias
}

def getReleaseKeyPassword() {
    checkVaultEnvironment()
    String keyPassword
    if (project.signingProps?.get("keyPassword") != null) {
        keyPassword = project.signingProps?.get("keyPassword")
    } else if (project.hasProperty("IGNORE_VAULT")) {
        //build release using dummy signature
        keyPassword = "dummypassword"
    } else {
        try {
            keyPassword = project?.vault?.get(secretKeyPassword)['data']['value']
        } catch (Exception e) {
            throw new InvalidUserDataException("There was an error while reading the key alias from vault.\n" +
                    "Make sure to have $secretKeyPassword in the vault.\n" +
                    "Run this task with -PIGNORE_VAULT to skip secrets and build a release apk using a dummy signature", e)
        }
    }
    return keyPassword
}

def checkLocalSigningData() {
    def propFile = file("${rootDir}/signing.properties")
    if (fileReadable(propFile)) {

        Properties signingProps = new Properties()
        propFile.withInputStream {
            signingProps.load(it)
        }
        project.ext.signingProps = signingProps
    } else {
        project.ext.signingProps = null
    }
}

def checkVaultEnvironment() {

    if (project.hasProperty("IGNORE_VAULT")) {
        //build release using dummy signature
        return
    }

    File tokenFile = new File("${System.env.HOME}/.vault-token")

    if (fileReadable(tokenFile)) {
        if (System.env.VAULT_TOKEN == null || System.env.VAULT_TOKEN == "") {
            project?.vault?.token = tokenFile.text
        }
    }

    if (System.env.VAULT_ADDR == null || (System.env.VAULT_TOKEN == null && !fileReadable(tokenFile))) {
        throw new InvalidUserDataException("Vault coordinates are not configured.\n" +
                "VAULT_ADDR and VAULT_TOKEN need to be set as environment variables.\n" +
                "Run this task with -PIGNORE_VAULT to skip secrets and build a release apk using a dummy signature")
    }
}

def doClearSecrets() {
    clearFields()
    project.ext.vaultKeystoreTempPath?.with { it ->
        def tempFile = file(it)
        fillWithGarbage(tempFile)
        println "deleting $tempFile"
        tempFile.delete()
        println "deleted $tempFile"
    }
    project.ext.vaultKeystoreTempPath = null
}

ext {
    getReleaseKeystore = this.&getReleaseKeystore
    getReleaseKeystorePassword = this.&getReleaseKeystorePassword
    getReleaseKeyAlias = this.&getReleaseKeyAlias
    getReleaseKeyPassword = this.&getReleaseKeyPassword
}

task prepareVault {
    checkLocalSigningData()
    doFirst {
        checkVaultEnvironment()
    }
}

task clearSecrets {
    doLast {
        doClearSecrets()
    }
}

project.afterEvaluate {
    Task signingTask = tasks.findByPath("validateSigningRelease")
    signingTask?.dependsOn prepareVault
    Task packageTask = tasks.findByPath("packageRelease")
    packageTask?.dependsOn prepareVault

    Task assemblyTask = tasks.findByPath("assembleRelease")
    assemblyTask?.dependsOn prepareVault
    assemblyTask?.finalizedBy clearSecrets
}

gradle.taskGraph.whenReady {
    gradle.taskGraph.allTasks[-1].doLast {
        doClearSecrets()
    }
}

